/*
 * generated by Xtext 2.14.0-SNAPSHOT
 */
package fr.inria.diverse.ale.repl.formatting2

import fr.inria.diverse.ale.repl.visitor2repl.Interpreter
import fr.inria.diverse.ale.repl.visitor2repl.REPL
import org.eclipse.xtext.formatting2.IFormattableDocument
import org.eclipse.xtext.formatting2.AbstractFormatter2
import fr.inria.diverse.ale.repl.visitor2repl.Instruction
import org.eclipse.emf.ecoretools.ale.Operation

class Visitor2replFormatter extends AbstractFormatter2 {

	def dispatch void format(REPL rEPL, extension IFormattableDocument document) {
		for (semicolon : rEPL.allRegionsFor.keywords(";")) {
			semicolon.surround[noSpace].append[newLine]
		}
		for (colon : rEPL.allRegionsFor.keywords(":")) {
			colon.surround[noSpace].append[newLine]
		}
		for (doublecolon : rEPL.allRegionsFor.keywords("::")) {
			doublecolon.surround[noSpace]
		}
		for (dot : rEPL.allRegionsFor.keywords(".")) {
			dot.surround[noSpace]
		}
		for (comma : rEPL.allRegionsFor.keywords(",")) {
			comma.prepend[noSpace]
		}
		for (parenthesispair : rEPL.allRegionsFor.keywordPairs("(", ")")) {
			parenthesispair.key.surround[noSpace]
			parenthesispair.value.prepend[noSpace]
		}
		for (curlypair : rEPL.allRegionsFor.keywordPairs("{", "}")) {
			curlypair.key.append[noSpace]
			curlypair.value.surround[noSpace]
			if (curlypair.key.nextSemanticRegion != curlypair.value) {
				curlypair.key.append[newLine]
				curlypair.value.append[newLine]
				curlypair.interior[indent]
			}
		}
		
		for (model : rEPL.model) {
			model.format
		}
		rEPL.interpreter.format
		for (instruction : rEPL.instructions) {
			instruction.format
		}
		
		rEPL.allRegionsFor.keyword("Sequence").append[noSpace; highPriority]
		rEPL.allRegionsFor.keyword("OrderedSet").append[noSpace; highPriority]
	}

	def dispatch void format(Interpreter interpreter, extension IFormattableDocument document) {
		interpreter.prepend[newLines=2]
		interpreter.interior[indent]
		if (interpreter.initMethod !== null) {
			interpreter.initMethod.format
		}
	}
	
	def dispatch void format(Instruction instruction, extension IFormattableDocument document) {
		instruction.prepend[newLines=2; highPriority]
		instruction.interior[indent]
		instruction.regionFor.keyword("=>").prepend[newLine]
	}
}
